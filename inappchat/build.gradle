plugins {
    id 'com.android.library'
    id 'maven-publish'
}

group = 'com.github.livechat'
version = libraryVersion

android {
    namespace 'com.livechatinc.inappchat'
    compileSdk 34
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 34
        buildConfigField "String", "VERSION_NAME", "\"${libraryVersion}\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            java {
                srcDir 'src/main/java'
            }
            resources {
                srcDir 'src/../lib'
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

publishing {
    publications {
        release(MavenPublication) {
            from components.findByName('release')
            groupId = 'com.github.livechat'
            artifactId = 'inappchat'
            version = libraryVersion
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation('androidx.annotation:annotation:1.9.1')
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.code.gson:gson:2.12.1'
    implementation 'com.android.volley:volley:1.2.1'
    testImplementation 'junit:junit:4.13.2'
}
